
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивОграничениеТипов = Новый Массив;
	МассивОграничениеТипов.Добавить(ТипЗнч(Параметры.СсылкаНаОбъект));
	
	ОграничениеТипов = Новый ОписаниеТипов(МассивОграничениеТипов);
	
	Элементы.ОбъектБД.ОграничениеТипа = ОграничениеТипов; 
	ОбъектБД = ОграничениеТипов.ПривестиЗначение(ОбъектБД); 

	ИмяОбъекта = ТипЗнч(ОбъектБД);
	
	ЭтаФорма.Заголовок = Параметры.ИмяОбъекта + "." + Параметры.ИмяТабличнойЧасти;
	
	ЗаполнитьИсходныеДанные();
	         	
КонецПроцедуры


&НаКлиенте
Процедура КолонкиПоискаИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.КолонкиПоиска.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы["ТаблицаОтбора" + ТекущиеДанные.ИмяРеквизита].Видимость = ТекущиеДанные.Использовать;

	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсходныеДанные()
	
	ОбъектБД = Параметры.СсылкаНаОбъект;
	
	ТаблицаОбъектаБД = ОбъектБД[Параметры.ИмяТабличнойЧасти].Выгрузить();
	
	КолонкиИсходнойТаблицы = ТаблицаОбъектаБД.Колонки;
			
	СгенерироватьТаблицуФормы(КолонкиИсходнойТаблицы, "ТаблицаОбъекта");
	
	Для Каждого СтрокаИсходнойТаблицы Из  ТаблицаОбъектаБД Цикл
		
		НоваяСтрока = ЭтотОбъект.ТаблицаОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСТрока, СтрокаИсходнойТаблицы);
			
	КонецЦикла; 
	
	КолонкиПоиска.Очистить();

	Для Каждого Колонка Из  КолонкиИсходнойТаблицы Цикл
		
		НоваяСтрока = КолонкиПоиска.Добавить();
		
		НоваяСтрока.ИмяРеквизита = Колонка.Имя;
		НоваяСтрока.ПредставлениеРеквизита = Колонка.Заголовок;
		НоваяСтрока.Использовать = Ложь;
		
		НоваяСтрока.Итоговый =  Колонка.ТипЗначения.СодержитТип(Тип("Число"))
			И не Колонка.Имя = "НомерСтроки"; 
		
	КонецЦикла;

	
	КолонкиИсходнойТаблицы.Вставить(0, "КоличествоНайденныхСтрок", Новый ОписаниеТипов("Число"), "Кол-во найденых строк", 10);
	
	СгенерироватьТаблицуФормы(КолонкиИсходнойТаблицы, "ТаблицаОтбора");
	
	Элементы.ТаблицаОтбора.УстановитьДействие("Перетаскивание","ТаблицаОтбораПеретаскивание");
	
		//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"ЗаполнитьСтроку", 
		Тип("КнопкаФормы"),   
		Элементы.ТаблицаОтбора.КонтекстноеМеню);          
		
	КнопкаФормы.ИмяКоманды = "ЗаполнитьСтроку";
	
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	
	
	Если Параметры.Хранилище <> Неопределено Тогда
		
		ДляЗаполнения = Параметры.Хранилище.Получить();
		
		ОтборКолонкиПоиска = Новый Структура("ИмяРеквизита");
		
		Для Каждого СтрокаДанных Из ДляЗаполнения.КолонкиПоиска Цикл
			
			ОтборКолонкиПоиска.ИмяРеквизита = СтрокаДанных.ИмяРеквизита;
			
			СтрокаКолонкиПоиска = ЭтотОбъект.КолонкиПоиска.НайтиСтроки(ОтборКолонкиПоиска);
			
			Если СтрокаКолонкиПоиска.Количество() <> 0 Тогда
				ЗаполнитьЗначенияСвойств(СтрокаКолонкиПоиска[0], СтрокаДанных, , "ИмяРеквизита");
			КонецЕсли;
						
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыОтбора Из ДляЗаполнения.ТаблицаОтбора Цикл
			
			НоваяСтрока = ЭтотОбъект.ТаблицаОтбора.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицыОтбора);
					
		КонецЦикла;
		
	КонецЕсли;   
	
	Для каждого Колонка Из КолонкиПоиска Цикл
		 		
		Элементы["ТаблицаОтбора" + Колонка.ИмяРеквизита].Видимость = Колонка.Использовать;
				
	КонецЦикла;
	 	
КонецПроцедуры

&НаСервере                                                                   
Процедура СгенерироватьТаблицуФормы(КолонкиИсходнойТаблицы, ИмяТаблицыФормы)
	
	
	МассивТипа = Новый Массив;
    МассивТипа.Добавить(Тип("ТаблицаЗначений"));

    // Добавление ТаблицыЗначений в массив реквизитов
    ОписаниеТипа = Новый ОписаниеТипов(МассивТипа);
    МассивРеквизитовФормы = Новый Массив;
    МассивРеквизитовФормы.Добавить(Новый РеквизитФормы(ИмяТаблицыФормы, ОписаниеТипа, "", ИмяТаблицыФормы));

	Для Каждого Колонка Из КолонкиИсходнойТаблицы Цикл
        МассивРеквизитовФормы.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, ИмяТаблицыФормы));
    КонецЦикла;

    // Удаление - если таблица существует
    ЭлементТаблица = Элементы.Найти(ИмяТаблицыФормы);
    Если ЭлементТаблица <> Неопределено Тогда
        Элементы.Удалить(ЭлементТаблица);
    Иначе
        ИзменитьРеквизиты(МассивРеквизитовФормы);
    КонецЕсли;

    // Добавление ТаблицыЗначений на форму
    ТаблицаПолейВыбора = Элементы.Добавить(ИмяТаблицыФормы, Тип("ТаблицаФормы"));
    ТаблицаПолейВыбора.ПутьКДанным = ИмяТаблицыФормы;
    ТаблицаПолейВыбора.Отображение = ОтображениеТаблицы.Список;
    ТаблицаПолейВыбора.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет; // Отключение командной панели

	Для Каждого Колонка Из КолонкиИсходнойТаблицы Цикл
		
        НовыйЭлемент = Элементы.Добавить(ИмяТаблицыФормы + Колонка.Имя, Тип("ПолеФормы"), ТаблицаПолейВыбора);
        НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
        НовыйЭлемент.ПутьКДанным = ИмяТаблицыФормы + "." + Колонка.Имя;
        НовыйЭлемент.Ширина = 10;

	КонецЦикла;
	 	                                                                                           
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьТаблицуОтборов()
	
	МассивКолонкиКВыгрузке = Новый Массив;
			
	ИтоговыеКолонки = Новый Массив;
	КолонкиОтбора = Новый Массив;
		
	Для Каждого Колонка Из ЭтотОбъект.КолонкиПоиска Цикл
		
		Если Не Колонка.Использовать Тогда			
			Продолжить;
		КонецЕсли;
		
		Если Колонка.Итоговый Тогда
			ИтоговыеКолонки.Добавить(Колонка.ИмяРеквизита);
		Иначе
			КолонкиОтбора.Добавить(Колонка.ИмяРеквизита);	
		КонецЕсли;
		
					
		МассивКолонкиКВыгрузке.Добавить(Колонка.ИмяРеквизита);
			
	КонецЦикла;

	
	
	МассивКолонкиКВыгрузке.Добавить("КоличествоНайденныхСтрок");
	
	КолонкиКВыгрузке = СтрСоединить(МассивКолонкиКВыгрузке, ", ");
	
	ИтоговаяТаблица = ЭтотОбъект.ТаблицаОтбора.Выгрузить(, КолонкиКВыгрузке);
	
	
	Результат = Новый Структура;
	Результат.Вставить("КолонкиОтбора", КолонкиОтбора);
	Результат.Вставить("ИтоговыеКолонки", ИтоговыеКолонки);
	Результат.Вставить("ТаблицаОтбораРезультат", ИтоговаяТаблица);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСтрокуНаСервере()
	
	МассивКлючей = Новый Массив;
	
	МассивИтоговыхПолей = Новый Массив;
	
	Для каждого РеквизитОтбора Из КолонкиПоиска Цикл
		
		Если Не РеквизитОтбора.Использовать Или РеквизитОтбора.Итоговый Тогда
			
			Если РеквизитОтбора.Использовать И РеквизитОтбора.Итоговый Тогда
				
				МассивИтоговыхПолей.Добавить(РеквизитОтбора.ИмяРеквизита);
				
			КонецЕсли;
				
			
			Продолжить;
		КонецЕсли;
			
		МассивКлючей.Добавить(РеквизитОтбора.ИмяРеквизита);
		
		
	КонецЦикла;
	
	ТекстКлючейСтруктурыПоиска  = СтрСоединить(МассивКлючей, ", ");
	
	Отбор = Новый Структура(ТекстКлючейСтруктурыПоиска);
	
	
	
	СтрокаОтбора = ЭтотОбъект.ТаблицаОтбора.НайтиПоИдентификатору(Элементы.ТаблицаОтбора.ТекущаяСтрока);
	
	ЗаполнитьЗначенияСвойств(Отбор, СтрокаОтбора);
	
	НайденныеСтроки = ЭтотОбъект.ТаблицаОбъекта.НайтиСтроки(Отбор);
	
	СтрокаОтбора.КоличествоНайденныхСтрок = НайденныеСтроки.Количество();
	
	Для Каждого ИтоговоеПоле Из МассивИтоговыхПолей Цикл
		
		СтрокаОтбора[ИтоговоеПоле] = 0;
						
	КонецЦикла;

	
	Для каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		
		Для Каждого ИтоговоеПоле Из МассивИтоговыхПолей Цикл
			
			СтрокаОтбора[ИтоговоеПоле] = СтрокаОтбора[ИтоговоеПоле] + НайденнаяСтрока[ИтоговоеПоле];
						
		КонецЦикла;
				
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОтбораПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Для Каждого ЗначениеПеретаскивая из ПараметрыПеретаскивания.Значение Цикл
		
		НоваяСтрока = ЭтотОбъект.ТаблицаОтбора.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеПеретаскивая);
		
	КонецЦикла;
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьСтроку(Команда)
	ЗаполнитьСтрокуНаСервере();
КонецПроцедуры

&НаСервере
Процедура Команда1НаСервере()
	
	ПодготовленныеДанные = ПодготовитьТаблицуОтборов();
	
	СтруктураСериализации = Новый Структура();
	СтруктураСериализации.Вставить("Версия", "1");
	СтруктураСериализации.Вставить("ИмяТаблицыОбъекта", Параметры.ИмяТабличнойЧасти);
	СтруктураСериализации.Вставить("ОбъектБД", ОбъектБД);
	СтруктураСериализации.Вставить("ТаблицаОтбора", ПодготовленныеДанные.ТаблицаОтбораРезультат);
	СтруктураСериализации.Вставить("КолонкиПоиска", КолонкиПоиска.Выгрузить());	
	СтруктураСериализации.Вставить("КолонкиОтбора", ПодготовленныеДанные.КолонкиОтбора);
	СтруктураСериализации.Вставить("ИтоговыеКолонки", ПодготовленныеДанные.ИтоговыеКолонки);
	                                                                      	
	Хранилище = Новый ХранилищеЗначения(СтруктураСериализации);
	                     
КонецПроцедуры

&НаКлиенте
Процедура Команда1(Команда)
	
	Команда1НаСервере();
	
	ОповеститьОВыборе(Хранилище);
	
КонецПроцедуры
